apply plugin: 'spring-boot'
// Applying the spring-boot plugin pulls in a newer version of jedis that we can't use yet. The
// other ways to override versions (namely the subprojects.configurations.all.resolutionStrategy in
// build.gradle) didn't work.
ext['jedis.version'] = spinnaker.version('jedis')
apply plugin: 'spinnaker.package'

ext {
  springConfigLocation = System.getProperty('spring.config.location', "${System.getProperty('user.home')}/.spinnaker/")
  springProfiles = System.getProperty('spring.profiles.active', "test,local")
  repackage = System.getProperty('springBoot.repackage', "false")
}

tasks.withType(org.springframework.boot.gradle.run.BootRunTask) {
  systemProperty('spring.config.location', project.springConfigLocation)
  systemProperty('spring.profiles.active', project.springProfiles)
}

configurations.all {
  exclude group: 'javax.servlet', module: 'servlet-api'
  exclude group: "org.slf4j", module: "slf4j-log4j12"
}

dependencies {
  spinnaker.group('retrofitDefault')

  compileOnly spinnaker.dependency("lombok")

  compile spinnaker.dependency('bootActuator')
  compile spinnaker.dependency('bootWeb')
  compile spinnaker.dependency('kork')
  compile spinnaker.dependency('korkWeb')
  compile spinnaker.dependency("korkStackdriver")
  compile spinnaker.dependency("korkSwagger")

  compile project(':fiat-file')
  compile project(':fiat-github')
  compile project(':fiat-google-groups')
  compile project(':fiat-ldap')

  testCompile spinnaker.dependency('korkJedisTest')
  testCompile "org.skyscreamer:jsonassert:1.3.0"
}

applicationName = 'fiat'
tasks.bootRepackage.enabled = project.repackage
